plugins {
    id("java")
    id("java-library")
    id("com.github.johnrengelman.shadow") version "7.1.0"
}

group = 'de.weapie'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jfHomeRepoPrivate"
        url = "https://repo.jf.home/private"
        credentials(PasswordCredentials)
    }
}

dependencies {
    compileOnly project.libs.paperapi

    implementation project.libs.devlokFiles
    implementation project.libs.paperlib

    compileOnly project.libs.lombok
    annotationProcessor project.libs.lombok
    testCompileOnly project.libs.lombok
    testAnnotationProcessor project.libs.lombok
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveVersion.set(project.version)
    archiveFileName.set("${archiveBaseName.get()}-${archiveVersion.get()}.jar")

    relocate "io.papermc.lib", "de.weapie.randomtp.paperlib"
}